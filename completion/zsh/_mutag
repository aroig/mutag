#compdef paur paur.static=paur

# Zsh completion for paur, based on pacman completions.

# copy this file to /usr/share/zsh/site-functions/_paur

typeset -A opt_args

# options for passing to _arguments: main paur commands
_paur_opts_commands=(
	'-Q[Query the package database]'
	'-R[Remove a package from the system]'
	'-S[Synchronize packages]'
	'-U[Upgrade a package]'
	'-V[Display version and exit]'
	'-h[Display usage]'
)

# options for passing to _arguments: options common to all commands
_paur_opts_common=(
	'--noconfirm[Do not ask for confirmation]'
	'--arch[Restrict to Arch repositories]'
	'--aur[Restrict to AUR]'
	'--user[Restrict to User repository]'
)

# options for passing to _arguments: options for --upgrade commands
_paur_opts_pkgfile=(
	'-i[View package information]'
	'*:package file:_files -g "*.pkg.tar*(.)"'
)

# options for passing to _arguments: subactions for --query command
_paur_opts_query_actions=(
	'-o[Query the package that owns a file]:file:_files'
	'-s[Search package names and descriptions]:*:search text:->query_search'
)

# options for passing to _arguments: options for --query and subcommands
_paur_opts_query_modifiers=(
	'-i[View package information]'
	'-l[List package contents]'
	'-m[List installed packages not found in sync db(s)]'
	'-t[Print a dependency tree]'
	'-u[List packages that can be upgraded]'
)

# options for passing to _arguments: options for --remove command
_paur_opts_remove=(
	'-c[Remove all dependent packages]'
	'-d[Skip dependency checks]'
	'-n[Remove protected configuration files]'
	'-s[Remove dependencies not required by other packages]'
	'--dbonly[Only remove database entry, do not remove files]'
	'*:installed package:_paur_completions_installed_packages'
)

# options for passing to _arguments: options for --sync command
_paur_opts_sync_actions=(
	'*-c[Remove old packages from cache]:*:clean:->sync_clean'
	'*-cc[Remove all packages from cache]:*:clean:->sync_clean'
	'-g[View all members of a package group]:*:package groups:->sync_group'
	'-s[Search package names and descriptions]:*:search text:->sync_search'
	'--dbonly[Only remove database entry, do not remove files]'
	'--needed[Do not reinstall up to date packages]'
	'--recursive[Reinstall all dependencies of target packages]'
)

# options for passing to _arguments: options for --sync command
_paur_opts_sync_modifiers=(
	'-d[Skip dependency checks]'
	'-f[Overwrite conflicting files]'
	'-i[View package information]'
	'-l[List all packages in a repository]'
	'-p[Print download URIs for each package to be installed]'
	'-u[Upgrade all out-of-date packages]'
	'-w[Download packages only]'
	'-y[Download fresh package databases]'
	'*--ignore[Ignore a package upgrade]:package:
		_paur_completions_all_packages'
	'*--ignoregroup[Ignore a group upgrade]:package group:
		_paur_completions_all_groups'
	'--asdeps[Install packages as non-explicitly installed]'
	'--asexplicit[Install packages as explicitly installed]'
)

# handles --help subcommand
_paur_action_help() {
	_arguments -s : \
		"$_paur_opts_commands[@]"
}

# handles cases where no subcommand has yet been given
_paur_action_none() {
	_arguments -s : \
		"$_paur_opts_commands[@]"
}

# handles --query subcommand
_paur_action_query() {
	local context state line
	typeset -A opt_args

#	_arguments -s : \
#		"$_paur_opts_common[@]" \
#		"$_paur_opts_query_actions[@]" \
#		"$_paur_opts_query_modifiers[@]"

	case $state in
		query_file)
			_arguments -s : \
				"$_paur_opts_common[@]" \
				"$_paur_opts_query_modifiers[@]" \
				'*:package file:_files -g "*.pkg.tar*"'
				;;
		query_group)
			_arguments -s : \
				"$_paur_opts_common[@]" \
				"$_paur_opts_query_modifiers[@]" \
				'*:groups:_paur_completions_installed_groups'
				;;
		query_owner)
			_arguments -s : \
				"$_paur_opts_common[@]" \
				"$_paur_opts_query_modifiers[@]" \
				'*:file:_files'
				;;
		query_search)
			_arguments -s : \
				"$_paur_opts_common[@]" \
				"$_paur_opts_query_modifiers[@]" \
				'*:search text: '
				;;
		*)
			_arguments -s : \
				"$_paur_opts_common[@]" \
				"$_paur_opts_query_actions[@]" \
				"$_paur_opts_query_modifiers[@]" \
				'*:package:_paur_completions_installed_packages'
				;;
	esac
}

# handles --remove subcommand
_paur_action_remove() {
	_arguments -s : \
		"$_paur_opts_common[@]" \
		"$_paur_opts_remove[@]"
}

# handles --sync subcommand
_paur_action_sync() {
	local context state line
	typeset -A opt_args

#	_arguments -s : \
#		"$_paur_opts_common[@]" \
#		"$_paur_opts_sync_actions[@]" #\
#		#"$_paur_opts_sync_modifiers[@]"

	case $state in
		sync_clean)
			_arguments -s : \
				"$_paur_opts_common[@]" \
				"$_paur_opts_sync_modifiers[@]" \
				'*-c[Remove old packages from cache]' \
				;;
		sync_group)
			_arguments -s : \
				"$_paur_opts_common[@]" \
				"$_paur_opts_sync_modifiers[@]" \
				'*:package group:_paur_completions_all_groups'
				;;
		sync_search)
			_arguments -s : \
				"$_paur_opts_common[@]" \
				"$_paur_opts_sync_modifiers[@]" \
				'*:search text: '
				;;
		*)
			_arguments -s : \
				"$_paur_opts_common[@]" \
				"$_paur_opts_sync_modifiers[@]" \
				'*:package:_paur_completions_all_packages'
				;;
		esac
}

# handles --upgrade subcommand
_paur_action_upgrade() {
	_arguments -s : \
		"$_paur_opts_common[@]" \
		"$_paur_opts_pkgfile[@]"
}

# handles --version subcommand
_paur_action_version() {
	# no further arguments
	return 0
}

# provides completions for package groups
_paur_completions_all_groups() {
	local -a groups
	groups=( $(paur -Sg) )
	typeset -U groups
	compadd "$@" -a groups
}

# provides completions for packages available from repositories
# these can be specified as either 'package' or 'repository/package'
_paur_completions_all_packages() {
	local -a packages repositories packages_long

	if compset -P1 '*/*'; then
		packages=( $(paur -Sa ${words[CURRENT]%/*}) )
		typeset -U packages
		_wanted repo_packages expl "repository/package" compadd ${(@)packages}
	else
		packages=( $(paur -Sql) )
		typeset -U packages
		_wanted packages expl "packages" compadd - "${(@)packages}"

		repositories=(user ${(o)${${${(M)${(f)"$(</etc/paur.conf)"}:#\[*}/\[/}/\]/}:#options} aur)
		typeset -U repositories
		_wanted repo_packages expl "repository/package" compadd -S "/" $repositories
	fi
}


# provides completions for installed packages
_paur_completions_installed_packages() {
	local -a packages packages_long
	packages_long=(/var/lib/pacman/local/*(/))
	packages=( ${${packages_long#/var/lib/pacman/local/}%-*-*} )
	compadd "$@" -a packages
}

# provides completions for repository names
_paur_completions_repositories() {
	local -a repositories
	repositories=(user ${(o)${${${(M)${(f)"$(</etc/pacman.conf)"}:#\[*}/\[/}/\]/}:#options} aur)
	# Uniq the array
	typeset -U repositories
	compadd "$@" -a repositories
}

# main dispatcher
_paur() {
	case $words[2] in
		-Q*o*) # file
			_arguments -s : \
				"$_paur_opts_common[@]" \
				"$_paur_opts_query_modifiers[@]" \
				'*:package file:_files'
				;;
		-Q*)  _paur_action_query    ;;
		-R*)  _paur_action_remove   ;;
		-S*)  _paur_action_sync     ;;
		-U*)  _paur_action_upgrade  ;;
		-V*)  _paur_action_version  ;;
		-h*)  _paur_action_help     ;;
		-  )  _paur_action_none     ;;
		*  )  return 1              ;;
	esac
}

# run the main dispatcher
_paur "$@"
